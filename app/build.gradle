plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.kapt)
    alias(libs.plugins.sqlDelight)
}

android {
    namespace 'com.example.otchallenge'
    compileSdk 34

    defaultConfig {
        applicationId "com.example.otchallenge"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            def localProperties = new Properties()
            File file = rootProject.file("local.properties")
            if(file.exists()) {
                localProperties.load(new FileInputStream(file))
                def nyTimesApiKey = localProperties['nyTimesApiKey']
                buildConfigField "String", "nyTimesApiKey", "\"$nyTimesApiKey\""
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        buildConfig = true
    }
}

sqldelight {
    databases {
        Database { // This will be the name of the generated database class.
            packageName = "com.example.otchallenge"
        }
    }
}

dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.activity
    implementation libs.androidx.constraintlayout
    // dagger
    implementation libs.dagger
    kapt libs.dagger.compiler

    //retrofit
    implementation libs.retrofit
    implementation libs.retrofit.rx.adapter
    implementation libs.retrofit.moshi

    //glide
    implementation libs.glide

    //reactive x
    implementation libs.rx.android
    implementation libs.rx.java
    implementation libs.rx.kotlin

    //moshi
    kapt libs.moshi.codegen

    //logging
    implementation libs.timber

    //Persistence
    implementation libs.sqlDelight
    implementation libs.sqlDelight.rxJava

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}